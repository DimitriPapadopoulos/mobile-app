import app.eduroam.shared.Libs

apply plugin: 'kotlin-multiplatform'
apply plugin: 'kotlin-native-cocoapods'
apply plugin: 'kotlinx-serialization'
apply from: 'android.gradle'

group = "app.eduroam.shared"
version = "1.0"

kotlin {
    cocoapods {
        summary = 'The common multiplatform library used by both Android and iOS.'
        homepage = 'https://www.egeniq.com/'
    }

    targets {
        fromPreset(presets.android, 'android')

        def sdkName = System.getenv("SDK_NAME") ?: ""
        // Add platform switching to have IDE support.
        if (sdkName.startsWith("iphoneos")) {
            iosArm64("ios") {
                compilations.main {
                    kotlinOptions.freeCompilerArgs += "-Xobjc-generics"
                }
            }
        } else {
            iosX64("ios") {
                compilations.main {
                    kotlinOptions.freeCompilerArgs += "-Xobjc-generics"
                }
            }
        }

    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }

    sourceSets {
        all {
            languageSettings {
                useExperimentalAnnotation('kotlin.Experimental')
            }
        }
        commonMain {
            dependencies {
                implementation Libs.Kotlin.stdlibCommon
                implementation Libs.Kotlin.serialization
                implementation Libs.Kotlin.Coroutines.core
                implementation Libs.Koin.core
                implementation Libs.Ktor.clientCore
                implementation Libs.Ktor.clientSerialization
                implementation Libs.Ktor.contentNegotiation
                api Libs.Ktor.logging
                implementation Libs.Touchlab.stately
                api Libs.Touchlab.kermit
            }
        }
        iosMain {
            dependencies {
                implementation Libs.Ktor.clientiOS
            }
        }
    }
}

repositories {
    mavenCentral()
}
